let
    bootStrap = import <nixpkgs> {};
    rev = "07e2b59812de95deeedde95fb6ba22d581d12fbc";
    # hash generated by doing nix-prefetch-url --unpack https://github.com/NixOS/nixpkgs-channels/archive/{rev}.tar.gz
    hash = "1yxmv04v2dywk0a5lxvi9a2rrfq29nw8qsm33nc856impgxadpgf";
    src = builtins.fetchTarball
    {
      name = "nixpkgs-${rev}";
      url = "https://github.com/NixOS/nixpkgs-channels/archive/${rev}.tar.gz";
      sha256 = hash;
    };
    # set in shell when customized package set is set as <nixpkgs> to avoid looping on self
    overrideNixPkgsSrc = builtins.tryEval <override-nixpkgs-src>;
in
  if overrideNixPkgsSrc.success
    then
      overrideNixPkgsSrc.value
    else
       bootStrap.runCommand "nixpkgs-${rev}" {
        outputHashMode = "recursive";
        outputHashAlgo = "sha256";
        outputHash = hash;
        inherit src;
      }
      ''
      cp -r $src $out
      ''
